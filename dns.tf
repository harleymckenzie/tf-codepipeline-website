data "aws_route53_zone" "web" {
  name = var.hostname
  private_zone = false
}

resource "aws_route53_record" "hostname" {
  zone_id = var.hosted-zone
  name = var.hostname
  type = "A"

  alias {
    name                   = aws_lb.alb.dns_name
    zone_id                = aws_lb.alb.zone_id
    evaluate_target_health = true
  }
}

resource "aws_route53_record" "ssl-validation" {
    for_each = {
      for dvo in aws_acm_certificate.web.domain_validation_options : dvo.domain_name => {
        name   = dvo.resource_record_name
        record = dvo.resource_record_value
        type   = dvo.resource_record_type
      }
    }
  
  allow_overwrite = true
  name            = each.value.name
  records         = [each.value.record]
  ttl             = 60
  type            = each.value.type
  zone_id         = data.aws_route53_zone.web.zone_id
}

resource "aws_acm_certificate_validation" "web" {
  certificate_arn         = aws_acm_certificate.web.arn
  validation_record_fqdns = [for record in aws_route53_record.ssl-validation : record.fqdn]
}